# -*- mode: python; coding: utf-8 -*-

env = Environment()

# TODO move pkgconfig and configure boilerplate to another file

def CheckPKGConfig(context, version):
     context.Message( 'Checking for pkg-config... ' )
     ret = context.TryAction('pkg-config --atleast-pkgconfig-version=%s' % version)[0]
     context.Result( ret )
     return ret

def CheckPKG(context, name):
     context.Message( 'Checking for %s... ' % name )
     ret = context.TryAction('pkg-config --exists \'%s\'' % name)[0]
     context.Result( ret )
     return ret

conf = Configure(env, custom_tests = { 'CheckPKGConfig' : CheckPKGConfig,
                                       'CheckPKG' : CheckPKG })

if not conf.CheckPKGConfig('0.15.0'):
     print 'pkg-config >= 0.15.0 not found.'
     Exit(1)

if not conf.CheckPKG('jansson >= 2.2.1'):
     print 'jansson >= 2.2.1 not found.'
     Exit(1)

env = conf.Finish()

env.Append(CCFLAGS = "-O2 -Wall -std=gnu99")
env.ParseConfig('pkg-config --cflags --libs jansson')
env.Append(LIBS='m')

# Make just common code and exporter source, not the AVR code
env.Program('exporter', [Glob('src/*.c'), Glob('src/effects/lib/*.c'), Glob('src/exporter/*.c')])
